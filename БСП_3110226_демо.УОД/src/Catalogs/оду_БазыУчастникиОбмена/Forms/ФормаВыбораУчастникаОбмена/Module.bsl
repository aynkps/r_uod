
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивУчастниковОбмена = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "МассивУчастниковОбмена", Неопределено);
	АдресПараметрыРегистрации = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(Параметры, "АдресПараметрыРегистрации", Неопределено);

	Если МассивУчастниковОбмена = Неопределено Тогда
		ЗакрытьСразу = Истина
	Иначе
		Для каждого Стр Из МассивУчастниковОбмена Цикл
		
			НСтр = УчастникиОбъектаОбмена.Добавить();
			ЗаполнитьЗначенияСвойств(НСтр, Стр); 
			ДопРеквизиты = "Сервер, Наименование";
			СтрРеквизитов = пр_Общий.ЗначенияРеквизитовОбъекта(Стр.БазаПриемник, ДопРеквизиты);
			
			ЗаполнитьЗначенияСвойств(НСтр, СтрРеквизитов, ДопРеквизиты);
			//НСтр.БазаПриемник = Эл;
			//НСтр.Выбрать = Истина;
		
		КонецЦикла; 	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если ЗакрытьСразу = Истина Тогда
		ПодключитьОбработчикОжидания("ЗакрытьСразу", 0.1, Истина);
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_УчастникиОбъектаОбмена

&НаКлиенте
Процедура УчастникиОбъектаОбменаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура УчастникиОбъектаОбменаВыбратьПриИзменении(Элемент)
	
	ТекЭлемент = Элементы.УчастникиОбъектаОбмена.ТекущиеДанные;
	
	Если ТекЭлемент = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТекЭлемент.РаспределятьПодчиненнымУО = Истина И ТекЭлемент.Выбрать = Истина Тогда
	
		Для каждого Стр Из УчастникиОбъектаОбмена Цикл
			Если Стр.ПодчиненныйУчастникОбмена = Истина Тогда
				Стр.Выбрать = Ложь;			
			КонецЕсли;	
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВсе(Команда)
	УстановитьПризнакВыбран(Истина);
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьВсе(Команда)
	УстановитьПризнакВыбран(Ложь);
КонецПроцедуры


&НаКлиенте
Процедура ПрименитьИЗакрыть(Команда)
	
	ВыбранСтроки = УчастникиОбъектаОбмена.НайтиСтроки(Новый Структура("Выбрать", Истина)); 
	
	Если ВыбранСтроки.Количество() = 0 Тогда
		МассивУчастниковОбмена = Неопределено;	
	Иначе
		
		МассивУчастниковОбмена = Новый Массив;
		
		Для каждого Стр Из ВыбранСтроки Цикл
			
			Если Стр.Выбрать = Ложь Тогда
				Продолжить;
			КонецЕсли;
			
			МассивУчастниковОбмена.Добавить(Стр.БазаПриемник);	
			
		КонецЦикла; 
		
	КонецЕсли; 
	
	Закрыть(Новый Структура("МассивУчастниковОбмена, АдресПараметрыРегистрации", МассивУчастниковОбмена, АдресПараметрыРегистрации));	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗакрытьСразу()  Экспорт
	Закрыть();	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПризнакВыбран(Значение)
	Для каждого Стр Из УчастникиОбъектаОбмена Цикл
		Стр.Выбрать = Значение;
	КонецЦикла; 
КонецПроцедуры


#КонецОбласти

